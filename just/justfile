set quiet
set dotenv-load
set export
set allow-duplicate-recipes
set allow-duplicate-variables

alias t := test
alias s := serve
alias run := serve
alias m := manage
alias mi := migrate
alias mm := makemigrations
alias bake := makemigrations
alias md := dance

src := "src"

# List commands
_default:
    just --list --unsorted --justfile {{ justfile() }} --list-heading $'Available commands:\n'

# Set up base tools
setup:
  curl -LsSf https://astral.sh/uv/install.sh | sh
  uv tool install ruff
  uv tool install build
  uv tool install twine

# Update the project
update:
  uv sync --all-extras

# Lock the dependencies
lock:
  uv lock

# Lint the project
lint *ARGS='.':
  -uvx ruff check {{ ARGS }}

# Check the types in the project
type *ARGS='.':
  -uv run --all-extras mypy {{ ARGS }}  # need to run through uv to see installed dependencies

# Benchmark the project
benchmark:
  -uv run --all-extras pytest tests/benchmarks/ --benchmark-only --benchmark-compare

# Run the tests
test *ARGS='':
  -uv run --all-extras pytest {{ ARGS }}

# Run the tests with integration tests
test-integration *ARGS='':
  -uv run --all-extras pytest {{ ARGS }} -m integration

# Run all tests
test-all *ARGS='':
  -uv run --all-extras pytest {{ ARGS }} -m integration

# Run coverage on the code
coverage:
  -uv run --all-extras pytest --cov={{ src }}

# Run all the dev things
dev:
  just lint
  just type
  just coverage

# Run the development server
serve port='8000':
  just manage runserver 0:{{ port }}

# Run database migrations
migrate *ARGS='':
  just manage migrate {{ ARGS }}

# Make database migrations
makemigrations:
  just manage makemigrations

# Do the migrate dance (make migrations and run them)
dance:
  just makemigrations
  just migrate

# Manage
manage *ARGS='':
  uv run --all-extras python3 manage.py {{ ARGS }}

# Start a new Django project
project-create:
  just bootstrap
  uv run django-admin startproject project .
  
# Start a new app
app-create name:
  just bootstrap
  uv run django-admin startapp {{ name }}

# Run documentation site
docs-serve:
  uv run --all-extras sphinx-autobuild -W docs/source docs/build

# Build documentation site
docs-build:
  cp CHANGELOG.md docs/source/changelog.md
  uv run --all-extras sphinx-build -W docs/source docs/build

# Build the package
build:
  just test
  just docs-build
  rm -rf dist/*
  # uvx --from build pyproject-build --installer uv
  uv build --no-sources

# Build and publish the package to test PyPI and prod PyPI
publish:
  just build
  # uvx twine check dist/*
  # uvx twine upload -r testpypi dist/*
  # echo
  # uvx twine upload -r pypi dist/*  # need to look at uv publish, but not sure about support for test.pypi.org
  uv publish --index testpypi --token
  uv publish --index pypi
